import com.eriwen.gradle.css.tasks.MinifyCssTask
import com.eriwen.gradle.js.tasks.MinifyJsTask
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies { classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0' }
}

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'idea'
    id 'war'

    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'com.eriwen.gradle.js' version '2.14.1'
    id 'com.eriwen.gradle.css' version '2.14.0'

    id 'nebula.lint' version '9.3.4'
}

apply plugin: 'org.junit.platform.gradle.plugin'
//apply plugin: 'pmd'

group = "com.imcode.imcms"
version = "6.0.0-beta3-SNAPSHOT"

description = """imCMS"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = projectBuildSourceEncoding
    options.fork = javaCompileFork
}

repositories {
    mavenCentral()
    maven { url "http://repo.boundlessgeo.com/main/" }
    maven { url "http://repo.imcode.com/maven2" }
}

ext {
    springframeworkVersion = '4.3.13.RELEASE'
    solrVersion = '7.2.1'
    slf4jVersion = '1.7.25'
    junitVersion = '5.2.0'
    sharedManifest = manifest {
        attributes "Build-Date": new Date()
        attributes "Application-Version": project.version
        attributes "Build-Jdk": System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')'
        attributes "Built-With": "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}"
        attributes "Built-By": project.hasProperty('BF_USER') ? project.getProperty('BF_USER') : System.getProperty('user.name')
    }
}

dependencies {
    compile "com.drewnoakes:metadata-extractor:2.11.0"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.5"
    compile("com.imcode.db:imcode-db:0.7") {
        exclude(module: 'junit')
    }
    compile "com.imcode.ldap:imcode-ldap:0.1"
    compile("com.imcode.util:imcode-util:2.0.4") {
        exclude(module: "servlet-api")
        exclude(module: "commons-io")
    }
    compile("com.jcabi:jcabi-w3c:1.3") {
        exclude(module: "commons-io")
    }
    compile "commons-beanutils:commons-beanutils:1.9.3"
    compile "commons-dbutils:commons-dbutils:1.6"
    compile "commons-lang:commons-lang:2.6"
    compile("commons-validator:commons-validator:1.6") {
        exclude(module: "commons-logging")
    }
    compile "javax.inject:javax.inject:1"
    compile "javax.servlet:javax.servlet-api:3.1.0"
    compile "javax.servlet:jstl:1.2"
    compile "javax.servlet.jsp:javax.servlet.jsp-api:2.3.1"
    compile "javax.validation:validation-api:1.1.0.Final"
    compile "net.sf.ehcache:ehcache:2.7.0"
    compile("net.sf.ehcache:ehcache-web:2.0.4") {
        exclude(module: 'ehcache-core')
    }
    compile "org.apache.commons:commons-collections4:4.1"
    compile "org.apache.commons:commons-dbcp2:2.1.1"
    compile "org.apache.commons:commons-email:1.4"
    compile "org.apache.commons:commons-lang3:3.6"
    compile "org.apache.commons:commons-text:1.1"
    compile("org.apache.httpcomponents:httpclient:4.5.4") {
        exclude(module: "commons-logging")
    }
    compile "org.apache.solr:solr-core:${solrVersion}"
    compile("org.apache.tika:tika-parsers:1.18") {
        exclude(module: "commons-logging")
        exclude(module: "asm")
        exclude(module: "commons-io")
    }
    compile "org.aspectj:aspectjrt:1.8.13"
    compile "org.aspectj:aspectjweaver:1.8.13"
    compile("org.hibernate:hibernate-ehcache:4.3.11.Final") {
        exclude(module: "ehcache-core")
    }
    compile "org.hibernate:hibernate-validator:5.4.2.Final"
    compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.springframework:spring-aop:${springframeworkVersion}"
    compile("org.springframework:spring-core:${springframeworkVersion}") {
        exclude(module: 'commons-logging')
    }
    compile "org.springframework:spring-orm:${springframeworkVersion}"
    compile "org.springframework:spring-webmvc:${springframeworkVersion}"
    compile "org.springframework.data:spring-data-jpa:1.11.7.RELEASE"

    //Azure AD dependencies
    compile "com.microsoft.azure:adal4j:1.6.0"
    compile "com.nimbusds:oauth2-oidc-sdk:5.24.1"

    runtime "com.fasterxml:classmate:1.3.3"
    runtime "javax.el:javax.el-api:3.0.0"
    runtime "mysql:mysql-connector-java:5.1.38"
    runtime "org.hibernate:hibernate-entitymanager:4.3.11.Final"
    runtime "org.slf4j:slf4j-log4j12:${slf4jVersion}"

    testCompile "com.jayway.jsonpath:json-path-assert:2.2.0"
    testCompile "com.jcabi:jcabi-matchers:1.4"
    testCompile "org.dbunit:dbunit:2.5.1"
    testCompile "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testCompile "org.junit.vintage:junit-vintage-engine:${junitVersion}"
    testCompile "org.mockito:mockito-core:2.19.0"
    testCompile "org.mockito:mockito-junit-jupiter:2.19.0"
    testCompile "org.skyscreamer:jsonassert:1.5.0"
    testCompile "org.springframework:spring-test:${springframeworkVersion}"
}

//Migrated maven properties
def mavenProps = new Properties()

//Used to merge build and server properties file with replacement of inplace variables usage
def buildProps = new Properties()
def tmpProps = new Properties()

//Directory to hold temporary data
def tmpDir = "${buildDir}/tmp/"

//Translation Languages
def localizationLanguages = new ArrayList()

task copyPropertyFileFromTemplate(type: Copy) {
    from projectDir
    into projectDir
    include 'build.properties.template'
    rename '(.*).template', '$1'
    doLast {
        throw new GradleException(
                '`build.properties` file was just created, fill in required properties and run execution again'
        )
    }
}

task checkPropertyFile(type: Copy) {
    def buildProperties = new File("${projectDir}/build.properties")
    if (!buildProperties.exists()) {
        copyPropertyFileFromTemplate.execute()
    }
}

task initParams(dependsOn: checkPropertyFile) {
    mavenProps.setProperty("log4j.threshold", "info")
    mavenProps.setProperty("log4j.hibernate.level", "error")
    mavenProps.setProperty("log4j.console-appender.threshold", "off")

    localizationLanguages.add("en")
    localizationLanguages.add("sv")

    file("build.properties").withInputStream { buildProps.load(it) }
}

task copyProperties(type: Copy, dependsOn: initParams) {
    from "${projectDir}"
    include "build.properties"

    //Needed to perform this two times to replace inplace variables
    filter(ReplaceTokens, tokens: buildProps)
    filter(ReplaceTokens, tokens: buildProps)
    into tmpDir
}

task importProperties(dependsOn: copyProperties) {
    doLast {
        file("${tmpDir}/build.properties").withInputStream { tmpProps.load(it) }
    }
}

task explodedWar(type: Sync, dependsOn: war) {
    into "${buildDir}/libs/exploded/${project.name}-${project.version}.war/"
    with war
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allJava
    classifier 'sources'
    manifest {
        from sharedManifest
    }
}

task individualJsMinify(dependsOn: tasks.matching { Task task -> task.name.startsWith("dominifyJs") })
task individualCssMinify(dependsOn: tasks.matching { Task task -> task.name.startsWith("dominifyCss") })

task cleanTemporaryDir(type: Delete, dependsOn: war) {
//    Disabled due to wrong file merging. Should be possible enabled in future. But clean task remove it as well
//    delete tmpDir
}

//Task dependency settings
copyProperties.finalizedBy importProperties

tasks.war {
    it.dependsOn importProperties
    it.dependsOn test
    it.dependsOn individualJsMinify
    it.dependsOn individualCssMinify
    it.finalizedBy cleanTemporaryDir
}

//Plugins setting up
lombok {
    version = "1.18.0"
    sha256 = ""
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

//gradleLint.rules = ['all-dependency']

javascript.source {
    custom {
        js {
            srcDir 'src/main/webapp/'
            include "**/*.js"
            exclude "**/*.min.js"
        }
    }
}

css.source {
    custom {
        css {
            srcDir 'src/main/webapp/'
            include "**/*.css"
            exclude "**/*.min.css"
        }
    }
}

javascript.source.custom.js.files.eachWithIndex { jsFile, idx ->
    tasks.create(name: "dominifyJs${idx}", type: MinifyJsTask) {
        def tmpFile = new File("${projectDir}/src/main/webapp")
        def fileLocation = tmpFile.toPath().relativize(jsFile.toPath()).toFile()

        closure.compilationLevel = 'SIMPLE_OPTIMIZATIONS'

        source = jsFile
        dest = "${tmpDir}/jsMin/${fileLocation}"
    }
}

css.source.custom.css.files.eachWithIndex { cssFile, idx ->
    tasks.create(name: "dominifyCss${idx}", type: MinifyCssTask) {
        def tmpFile = new File("${projectDir}/src/main/webapp")
        def fileLocation = tmpFile.toPath().relativize(cssFile.toPath()).toFile()

        source = cssFile
        dest = "${tmpDir}/cssMin/${fileLocation}"
    }
}

jar.doFirst {
    manifest {
        from sharedManifest
    }
    classifier 'classes'
}

jshint {
    jshint.options.source = javascript.source.custom.js.files
    jshint.options.dest = "${buildDir}/jshint.out"
}

csslint {
    source = css.source.custom.css.files
    dest = "${buildDir}/csslint.out"
}

war {
    exclude "**/js/imcms/**/*.js"
    exclude "**/css/**/*.css"

    manifest {
        from sharedManifest
    }

    //Copying configuration files
    webInf {
        from "src/main/resources"
        from "src/main/resources-conf"

        include "server.properties"
        include "imcms_*.properties"
        include "*.conf"
        include "log4j.xml"
        include "*.json"
        into "/WEB-INF/conf"
        filter(ReplaceTokens, tokens: tmpProps)
        filter(ReplaceTokens, tokens: mavenProps, beginToken: '${', endToken: '}')
    }

    //Copying ehcache configuration files
    webInf {
        from "src/main/resources"
        include "ehcache.xml"
        include "ehcache.xsd"
        into "/WEB-INF/classes"
    }

    //Template localization
    def rootFolderForTemplates = "src/main/webapp/WEB-INF/templates/lang"
    def rootFolderForImcmsTemplates = "src/main/webapp/imcms/lang"

    def templatePatterns = new ArrayList()

    templatePatterns.add("**/*.jsp")
    templatePatterns.add("**/*.jspx")
    templatePatterns.add("**/*.htm")
    templatePatterns.add("**/*.html")

    def langProperties
    localizationLanguages.each { lang ->
        langProperties = new Properties()
        file("src/main/resources-conf/imcms_${lang}.properties").withInputStream { langProperties.load(it) }

        webInf {
            from rootFolderForTemplates
            include templatePatterns
            filter(ReplaceTokens, tokens: langProperties, beginToken: "<? ", endToken: " ?>")
            into "/WEB-INF/templates/${lang}"
        }

        webInf {
            from rootFolderForTemplates
            exclude templatePatterns
            into "/WEB-INF/templates/${lang}"
        }

        webInf {
            from rootFolderForImcmsTemplates
            include templatePatterns
            filter(ReplaceTokens, tokens: langProperties, beginToken: "<? ", endToken: " ?>")
            into "/imcms/${lang}"
        }

        webInf {
            from rootFolderForImcmsTemplates
            exclude templatePatterns
            into "/imcms/${lang}"
        }
    }

    webInf {
        from "${tmpDir}/jsMin/"
        into "/"
    }

    webInf {
        from "${tmpDir}/cssMin/"
        into "/"
    }

    webInf {
        def versionFile = new File("${tmpDir}/version.txt")
        doFirst {
            versionFile.text = version
        }
        from versionFile
        into "/WEB-INF/"
    }

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar

            //Ugly way to add war to publication with preserving correct final pom file
            //Haven't found solution with using components.web
            artifact war
        }
    }

    repositories {
        maven {
            credentials {
                username ""
                password ""
            }
            url "sftp://repo.imcode.com:2022/srv/www/repo.imcode.com/maven2"

// Configure it to make publish to appropriate folder depending on version
//            if(project.version.endsWith('-SNAPSHOT')) {
//                url "http://.../snapshots/"
//            } else {
//                url "http:/.../internal/"
//            }
        }
    }
}
