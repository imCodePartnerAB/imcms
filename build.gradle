import com.eriwen.gradle.css.tasks.MinifyCssTask
import com.eriwen.gradle.js.tasks.MinifyJsTask
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.netflix.nebula:gradle-lint-plugin:latest.release'
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.12'
    id "com.eriwen.gradle.js" version "2.14.1"
    id "com.eriwen.gradle.css" version "2.14.0"
    id 'nebula.lint' version '7.4.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'css'
apply plugin: "com.eriwen.gradle.js"

group = "com.imcode.imcms"
version = "6.0.0-beta2-SNAPSHOT"

description = """imCMS"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = projectBuildSourceEncoding
    options.fork = javaCompileFork
}

repositories {
    mavenCentral()
    maven { url "http://repo.boundlessgeo.com/main/" }
    maven { url "http://repo.imcode.com/maven2" }
}

ext {
    springframeworkVersion = '4.3.13.RELEASE'
    solrVersion = '7.2.1'
    slf4jVersion = '1.7.25'
}

dependencies {
    compile "org.projectlombok:lombok:1.16.20"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.apache.logging.log4j:log4j-core:2.10.0"
    compile "com.drewnoakes:metadata-extractor:2.11.0"
    compile "commons-beanutils:commons-beanutils:1.9.3"
    compile("org.apache.tika:tika-parsers:1.11") {
        exclude(module: "commons-logging")
        exclude(module: "asm")
        exclude(module: "commons-io")
    }
    compile("commons-validator:commons-validator:1.6") {
        exclude(module: "commons-logging")
    }
    compile "org.apache.commons:commons-collections4:4.1"
    compile "net.sf.ehcache:ehcache:2.7.0"
    compile "net.sf.ehcache:ehcache-web:2.0.4"
    compile("org.hibernate:hibernate-ehcache:4.3.11.Final") {
        exclude(module: "ehcache-core")
    }
    compile "commons-dbutils:commons-dbutils:1.6"
    compile "commons-lang:commons-lang:2.6"
    compile "org.apache.commons:commons-lang3:3.6"
    compile "org.apache.commons:commons-text:1.1"
    compile "org.apache.commons:commons-email:1.4"
    compile "org.hibernate:hibernate-entitymanager:4.3.11.Final"
    compile "org.hibernate:hibernate-validator:5.4.2.Final"
    compile "com.fasterxml:classmate:1.3.3"
    compile "com.imcode.db:imcode-db:0.5"
    compile "com.sun.xml.bind:jaxb-impl:2.3.0"
    compile "com.sun.xml.bind:jaxb-core:2.3.0"
    compile("com.imcode.util:imcode-util:2.0.4") {
        exclude(module: "servlet-api")
        exclude(module: "commons-io")
    }
    compile "com.imcode.ldap:imcode-ldap:0.1"
    compile "mysql:mysql-connector-java:5.1.38"
    compile "org.apache.commons:commons-dbcp2:2.1.1"
    compile("org.springframework:spring-core:${springframeworkVersion}")
    compile "org.springframework:spring-webmvc:${springframeworkVersion}"
    compile "org.springframework:spring-orm:${springframeworkVersion}"
    compile "org.springframework:spring-aop:${springframeworkVersion}"
    compile "org.aspectj:aspectjrt:1.8.13"
    compile "org.aspectj:aspectjweaver:1.8.13"
    compile("org.springframework.data:spring-data-jpa:1.11.7.RELEASE")
    compile "javax.servlet:javax.servlet-api:3.1.0"
    compile "org.apache.sanselan:sanselan:0.97-incubator"
    compile("org.apache.solr:solr-core:${solrVersion}")
    compile("org.apache.httpcomponents:httpclient:4.5.2") {
        exclude(module: "commons-logging")
    }
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.4"
    compile "javax.servlet:jstl:1.2"
    compile "org.apache.tomcat:tomcat-jsp-api:7.0.57"
    compile "org.apache.tomcat:tomcat-el-api:7.0.57"
    compile "org.apache.tomcat:tomcat-jasper-el:7.0.57"
    compile "javax.validation:validation-api:1.1.0.Final"
    compile "javax.inject:javax.inject:1"
    compile "org.jsoup:jsoup:1.10.2"
    compile "javax.json:javax.json-api:1.1.2"
    compile("com.jcabi:jcabi-w3c:1.3") {
        exclude(module: "commons-io")
    }
    runtime "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-all:1.10.19"
    testCompile "org.powermock:powermock-api-mockito:1.6.2"
    testCompile "org.springframework:spring-test:${springframeworkVersion}"
    testCompile "com.jcabi:jcabi-matchers:1.4"
    testCompile "com.jayway.jsonpath:json-path-assert:2.2.0"
    testCompile "org.skyscreamer:jsonassert:1.5.0"
}

//Migrated maven properties
def mavenProps = new Properties()

//Used to merge build and server properties file with replacement of inplace variables usage
def buildProps = new Properties()
def tmpProps = new Properties()

//Directory to hold temporary data
def tmpDir = "${buildDir}/tmp/"

//Translation Languages
def localizationLanguages = new ArrayList()

task copyPropertyFileFromTemplate(type: Copy) {
    from projectDir
    into projectDir
    include 'build.properties.template'
    rename '(.*).template', '$1'
    doLast {
        throw new GradleException(
                '`build.properties` file was just created, fill in required properties and run execution again'
        )
    }
}

task checkPropertyFile(type: Copy) {
    def buildProperties = new File("${projectDir}/build.properties")
    if (!buildProperties.exists()) {
        copyPropertyFileFromTemplate.execute()
    }
}

task initParams(dependsOn: checkPropertyFile) {
    mavenProps.setProperty("log4j.threshold", "info")
    mavenProps.setProperty("log4j.hibernate.level", "error")
    mavenProps.setProperty("log4j.console-appender.threshold", "off")

    localizationLanguages.add("eng")
    localizationLanguages.add("swe")

    file("build.properties").withInputStream { buildProps.load(it) }
}

task copyProperties(type: Copy, dependsOn: initParams) {
    from "${projectDir}"
    include "build.properties"

    //Needed to perform this two times to replace inplace variables
    filter(ReplaceTokens, tokens: buildProps)
    filter(ReplaceTokens, tokens: buildProps)
    into tmpDir
}

task importProperties(dependsOn: copyProperties) {
    doLast {
        file("${tmpDir}/build.properties").withInputStream { tmpProps.load(it) }
    }
}

task explodedWar(type: Sync, dependsOn: war) {
    into "${buildDir}/exploded"
    with war
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task individualJsMinify(dependsOn: tasks.matching { Task task -> task.name.startsWith("dominifyJs") })
task individualCssMinify(dependsOn: tasks.matching { Task task -> task.name.startsWith("dominifyCss") })

//Task dependency settings
copyProperties.finalizedBy importProperties
tasks.war.dependsOn importProperties
tasks.war.dependsOn test
tasks.war.dependsOn individualJsMinify
tasks.war.dependsOn individualCssMinify

//Plugins setting up
lombok {
    version = "1.16.20"
    sha256 = ""
}

idea {
    module {
        inheritOutputDirs = false
    }
}

gradleLint.rules = ['all-dependency']

javascript.source {
    custom {
        js {
            srcDir 'src/main/webapp/'
            include "**/*.js"
            exclude "**/*.min.js"
        }
    }
}

css.source {
    custom {
        css {
            srcDir 'src/main/webapp/'
            include "**/*.css"
            exclude "**/*.min.css"
        }
    }
}

javascript.source.custom.js.files.eachWithIndex { jsFile, idx ->
    tasks.create(name: "dominifyJs${idx}", type: MinifyJsTask) {
        def tmpFile = new File("${projectDir}/src/main/webapp")
        def fileLocation = tmpFile.toPath().relativize(jsFile.toPath()).toFile()

        source = jsFile
        dest = "${tmpDir}/jsMin/${fileLocation}"
    }
}

css.source.custom.css.files.eachWithIndex { cssFile, idx ->
    tasks.create(name: "dominifyCss${idx}", type: MinifyCssTask) {
        def tmpFile = new File("${projectDir}/src/main/webapp")
        def fileLocation = tmpFile.toPath().relativize(cssFile.toPath()).toFile()

        source = cssFile
        dest = "${tmpDir}/cssMin/${fileLocation}"
    }
}

jar.doFirst {
    manifest {
        def manifestFile = "${projectDir}/META-INF/MANIFEST.MF"
        if (new File(manifestFile).exists())
            from(manifestFile)

        attributes "Build-Date": new Date()
        attributes "Application-Version": project.version
        attributes "Build-Jdk": System.getProperty("java.version") + " (" + System.getProperty("java.vendor") + ")"
        attributes "Built-With": "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}"
        attributes "Built-By": project.hasProperty("BF_USER") ? project.getProperty("BF_USER") : System.getProperty("user.name")
    }
}

jshint {
    jshint.options.source = javascript.source.custom.js.files
    jshint.options.dest = "${buildDir}/jshint.out"
}

csslint {
    source = css.source.custom.css.files
    dest = "${buildDir}/csslint.out"
}

war {
    classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime
//    duplicatesStrategy "include"

    manifest {
        def manifestFile = "${projectDir}/META-INF/MANIFEST.MF"
        if (new File(manifestFile).exists())
            from(manifestFile)

        attributes "Build-Date": new Date()
        attributes "Application-Version": project.version
        attributes "Build-Jdk": System.getProperty("java.version") + " (" + System.getProperty("java.vendor") + ")"
        attributes "Built-With": "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}"
        attributes "Built-By": project.hasProperty("BF_USER") ? project.getProperty("BF_USER") : System.getProperty("user.name")
    }

    //Copying configuration files
    webInf {
        from "src/main/resources"
        from "src/main/resources-conf"

        include "server.properties"
        include "imcms_*.properties"
        include "*.conf"
        include "log4j.xml"
        include "*.json"
        into "/WEB-INF/conf"
        filter(ReplaceTokens, tokens: tmpProps)
        filter(ReplaceTokens, tokens: mavenProps, beginToken: '${', endToken: '}')
    }

    //Copying ehcache configuration files
    webInf {
        from "src/main/resources"
        include "ehcache.xml"
        include "ehcache.xsd"
        into "/WEB-INF/classes"
    }

    //Template localization
    def rootFolderForTemplates = "src/main/webapp/WEB-INF/templates/lang"
    def rootFolderForImcmsTemplates = "src/main/webapp/imcms/lang"

    def templatePatterns = new ArrayList()

    templatePatterns.add("**/*.jsp")
    templatePatterns.add("**/*.jspx")
    templatePatterns.add("**/*.htm")
    templatePatterns.add("**/*.html")

    def langProperties
    localizationLanguages.each { lang ->
        langProperties = new Properties()
        file("src/main/resources-conf/imcms_${lang}.properties").withInputStream { langProperties.load(it) }

        webInf {
            from rootFolderForTemplates
            include templatePatterns
            filter(ReplaceTokens, tokens: langProperties, beginToken: "<? ", endToken: " ?>")
            into "/WEB-INF/templates/${lang}"
        }

        webInf {
            from rootFolderForTemplates
            exclude templatePatterns
            into "/WEB-INF/templates/${lang}"
        }

        webInf {
            from rootFolderForImcmsTemplates
            include templatePatterns
            filter(ReplaceTokens, tokens: langProperties, beginToken: "<? ", endToken: " ?>")
            into "/imcms/${lang}"
        }

        webInf {
            from rootFolderForImcmsTemplates
            exclude templatePatterns
            into "/imcms/${lang}"
        }
    }

    webInf {
        doLast {
            from "${tmpDir}/jsMin/"
            into "/"
        }
    }

    webInf {
        doLast {
            from "${tmpDir}/cssMin/"
            into "/"
        }
    }

    webInf {
        def versionFile = new File("${tmpDir}/version.txt")
        doFirst {
            versionFile.text = version
        }
        from versionFile
        into "/WEB-INF/"
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
        mavenWeb(MavenPublication) {
            from components.web
        }
    }

//    repositories {
//        maven {
//            credentials {
//                username ""
//                password ""
//            }
//
//            if(project.version.endsWith('-SNAPSHOT')) {
//                url "http://.../snapshots/"
//            } else {
//                url "http:/.../internal/"
//            }
//        }
//    }
}